plugins {
    id 'com.android.application'
}

apply from:"$projectDir/../config.gradle"

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOS_VERSION
    ndkVersion NDK_VERSION
    defaultConfig {
        applicationId "com.pxy.larkxr_openxr_pico"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 1
        versionName "3.2.2.4"

        shaders {
            glslcArgs.addAll(['-c', '-g'])
        }
        externalNativeBuild {
            cmake {
                targets 'lark_xr_openxr_pico'
                cppFlags "-frtti -fexceptions -std=c++1y -DANDROID_NDK_BUILD=1"
                abiFilters "arm64-v8a"
                if (BUILD_WITH_CLOUDXR_SUPPORT) {
                    arguments "-DANDROID_STL=c++_shared",
                            "-DCLOUDXR_LIBPATH=${project.rootDir}/third_party/cloudxr/CloudXR/jni",
                            "-DCLOUDXR_INCLUDE=${project.rootDir}/third_party/cloudxr/CloudXR/include",
                            "-DENABLE_CLOUDXR=1"
                } else {
                    arguments "-DANDROID_STL=c++_shared"
                }
            }
        }
    }

    buildTypes {
        String ENABLE_CLOUDXR = BUILD_WITH_CLOUDXR_SUPPORT ? "true" : "false";
        debug {
            buildConfigField "boolean","ENABLE_CLOUDXR",ENABLE_CLOUDXR
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean","ENABLE_CLOUDXR",ENABLE_CLOUDXR
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            def base = "../"
            def lib_prex = "lib/android/"
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = [
            ]
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    // gradle 7+
    packagingOptions {
        jniLibs {
            pickFirsts += [
                    'lib/arm64-v8a/libglm.so',
                    'lib/armeabi-v7a/libglm.so',
                    'lib/arm64-v8a/libPocoFoundation.so',
                    'lib/armeabi-v7a/libPocoFoundation.so',
                    'lib/arm64-v8a/libCloudXRClient.so',
                    'lib/armeabi-v7a/libCloudXRClient.so'
            ]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
//            def date = new Date().format("mmss");
            def build = 1
            def abi = "all";
            if (output.filters[0] != null) {
                abi = output.filters[0].identifier
            }
            def type = "pico-openxr";

            outputFileName =  "lark-xr-" +
                    defaultConfig.versionName + "-build-" +
                    build + "-" + type + "-beta-" + abi + ".apk"
        }
    }


    repositories {
        flatDir {
            dirs 'libs', "../lark_xr"
        }
    }
}

dependencies {
    implementation project(':lib_pxygl')
    implementation project(':lib_xr_common_ui')
    implementation(name: 'larkxr-kit-3.2.2.0-build-1', ext: 'aar')

    // WARNING
    // enable cloudxr support must put your CloudXR.aar under larkar_android/libs dir
    if (BUILD_WITH_CLOUDXR_SUPPORT) {
        implementation files("${project.rootDir}/third_party/cloudxr/CloudXR.aar")
    }
}