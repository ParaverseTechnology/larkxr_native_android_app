apply plugin: 'com.android.application'

apply from:"$projectDir/../config.gradle"

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOS_VERSION
    ndkVersion NDK_VERSION
//    ndkVersion '22.1.7171670'
    defaultConfig {
        applicationId "com.pxy.cloudlarkxrpico"
        minSdkVersion 26
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 1
        versionName "3.1.8.7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-frtti -fexceptions -std=c++11 -DANDROID_NDK_BUILD=1"
                arguments "-DANDROID_STL=c++_shared"
                targets 'lark_xr_pico'
            }
            ndk {
                abiFilters 'armeabi-v7a','arm64-v8a'
            }
            ndkBuild {
                abiFilters 'armeabi-v7a','arm64-v8a'
            }
        }
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    sourceSets {
        main {
            def base = "../"
            def lib_prex = "lib/android/"
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = [
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    repositories {
        flatDir {
            dirs 'libs', '../lark_xr'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
//            def date = new Date().format("mmss");
            def build = 1
            def abi = "all";
            if (output.filters[0] != null) {
                abi = output.filters[0].identifier
            }
            def type = "pico";
            outputFileName =  "cloudlark-xr-" +
                    defaultConfig.versionName + "-build-" +
                    build + "-" + type + "-beta-" + abi + ".apk"
        }
    }

    packagingOptions {
        pickFirst "lib/arm64-v8a/libglm.so"
        pickFirst "lib/armeabi-v7a/libglm.so"

        pickFirst "lib/arm64-v8a/libPocoFoundation.so"
        pickFirst "lib/armeabi-v7a/libPocoFoundation.so"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation (name:'PvrSDK-Native-20210730', ext:'aar')
    //
    implementation project(':lib_pxygl')
    implementation project(':lib_xr_common_ui')
    implementation(name: 'larkxr-kit-3.1.8.1-build-1', ext: 'aar')
}
